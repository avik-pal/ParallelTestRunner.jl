using ParallelTestRunner
using Test

cd(@__DIR__)

include(joinpath(@__DIR__, "utils.jl"))

@testset "ParallelTestRunner" verbose=true begin

@testset "basic use" begin
    io = IOBuffer()
    io_color = IOContext(io, :color => true)
    runtests(ParallelTestRunner, ["--verbose"]; stdout=io_color, stderr=io_color)
    str = String(take!(io))

    println()
    println("Showing the output of one test run:")
    println("-"^80)
    print(str)
    println("-"^80)
    println()

    @test contains(str, "SUCCESS")

    # --verbose output
    @test contains(str, r"basic .+ started at")

    @test contains(str, "time (s)")

    @test contains(str, "Available memory:")
    @test contains(str, "Init")

     # compile time as part of the struct not available before 1.11
    if VERSION >= v"1.11"
        @test contains(str, "Compile")
        @test contains(str, "(%)")
    end

    @test isfile(ParallelTestRunner.get_history_file(ParallelTestRunner))
end

@testset "default njobs" begin
    @test ParallelTestRunner.default_njobs(; cpu_threads=4, free_memory=UInt64(2) ^ 28) == 1
    @test ParallelTestRunner.default_njobs(; cpu_threads=4, free_memory=UInt64(2) ^ 30) == 1
    @test ParallelTestRunner.default_njobs(; cpu_threads=4, free_memory=UInt64(2) ^ 31) == 1
    @test ParallelTestRunner.default_njobs(; cpu_threads=4, free_memory=UInt64(2) ^ 32) == 2
    @test ParallelTestRunner.default_njobs(; cpu_threads=4, free_memory=UInt64(2) ^ 33) == 4
    @test ParallelTestRunner.default_njobs(; cpu_threads=4, free_memory=UInt64(2) ^ 34) == 4
end

@testset "subdir use" begin
    d = @__DIR__
    testsuite = find_tests(d)
    @test last(testsuite["basic"].args) == joinpath(d, "basic.jl")
    @test last(testsuite["subdir/subdir_test"].args) == joinpath(d, "subdir", "subdir_test.jl")
end

@testset "custom tests" begin
    testsuite = Dict(
        "custom" => quote
            @test true
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test !contains(str, r"basic .+ started at")
    @test contains(str, r"custom .+ started at")
    @test contains(str, "SUCCESS")
end

@testset "init code" begin
    init_code = quote
        using Test
        should_be_defined() = true

        macro should_also_be_defined()
            return :(true)
        end
    end
    testsuite = Dict(
        "custom" => quote
            @test should_be_defined()
            @test @should_also_be_defined()
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; init_code, testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"custom .+ started at")
    @test contains(str, "SUCCESS")
end

@testset "init worker code" begin
    init_worker_code = quote
        should_be_defined() = true

        macro should_also_be_defined()
            return :(true)
        end
    end
    init_code = quote
        using Test
        import ..should_be_defined, ..@should_also_be_defined
    end

    testsuite = Dict(
        "custom" => quote
            @test should_be_defined()
            @test @should_also_be_defined()
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; init_code, init_worker_code, testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"custom .+ started at")
    @test contains(str, "SUCCESS")
end

@testset "custom worker" begin
    function test_worker(name)
        if name == "needs env var"
            return addworker(env = ["SPECIAL_ENV_VAR" => "42"])
        elseif name == "threads/2"
            return addworker(exeflags = ["--threads=2"])
        end
        return nothing
    end
    testsuite = Dict(
        "needs env var" => quote
            @test ENV["SPECIAL_ENV_VAR"] == "42"
        end,
        "doesn't need env var" => quote
            @test !haskey(ENV, "SPECIAL_ENV_VAR")
        end,
        "threads/1" => quote
            @test Base.Threads.nthreads() == 1
        end,
        "threads/2" => quote
            @test Base.Threads.nthreads() == 2
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; test_worker, testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"needs env var .+ started at")
    @test contains(str, r"doesn't need env var .+ started at")
    @test contains(str, r"threads/1 .+ started at")
    @test contains(str, r"threads/2 .+ started at")
    @test contains(str, "SUCCESS")
end

@testset "custom worker with `init_worker_code`" begin
    init_worker_code = quote
        should_be_defined() = true
    end
    init_code = quote
        using Test
        import ..should_be_defined
    end
    function test_worker(name, init_worker_code)
        if name == "needs env var"
            return addworker(env = ["SPECIAL_ENV_VAR" => "42"]; init_worker_code)
        elseif name == "threads/2"
            return addworker(exeflags = ["--threads=2"]; init_worker_code)
        end
        return nothing
    end
    testsuite = Dict(
        "needs env var" => quote
            @test ENV["SPECIAL_ENV_VAR"] == "42"
            @test should_be_defined()
        end,
        "doesn't need env var" => quote
            @test !haskey(ENV, "SPECIAL_ENV_VAR")
            @test should_be_defined()
        end,
        "threads/1" => quote
            @test Base.Threads.nthreads() == 1
            @test should_be_defined()
        end,
        "threads/2" => quote
            @test Base.Threads.nthreads() == 2
            @test should_be_defined()
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; test_worker, init_code, init_worker_code, testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"needs env var .+ started at")
    @test contains(str, r"doesn't need env var .+ started at")
    @test contains(str, r"threads/1 .+ started at")
    @test contains(str, r"threads/2 .+ started at")
    @test contains(str, "SUCCESS")
end

@testset "failing test" begin
    testsuite = Dict(
        "failing test" => quote
            println("This test will fail")
            @test 1 == 2
        end
    )
    error_line = @__LINE__() - 3

    io = IOBuffer()
    ioc = IOContext(io, :color => true)
    @test_throws Test.FallbackTestSetException("Test run finished with errors") begin
        runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=ioc, stderr=ioc)
    end

    str = String(take!(io))
    @test contains(str, r"failing test.+ failed at")
    @test contains(str, "$(basename(@__FILE__)):$error_line")
    @test contains(str, "FAILURE")
    @test contains(str, "Output generated during execution of '\e[31mfailing test\e[39m':")
    @test contains(str, "Test Failed")
    @test contains(str, "1 == 2")
end

@testset "nested failure" begin
    testsuite = Dict(
        "nested" => quote
            @test true
            @testset "foo" begin
                @test true
                @testset "bar" begin
                    @test false
                end
            end
        end
    )
    error_line = @__LINE__() - 5

    io = IOBuffer()
    @test_throws Test.FallbackTestSetException("Test run finished with errors") begin
        runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=io, stderr=io)
    end

    str = String(take!(io))
    @test contains(str, r"nested .+ started at")
    @test contains(str, r"nested .+ failed at")
    @test contains(str, r"nested .+ \| .+ 2 .+ 1 .+ 3")
    @test contains(str, r"foo .+ \| .+ 1 .+ 1 .+ 2")
    @test contains(str, r"bar .+ \| .+ 1 .+ 1")
    @test contains(str, "FAILURE")
    @test contains(str, "Error in testset bar")
    @test contains(str, "$(basename(@__FILE__)):$error_line")
end

@testset "throwing test" begin
    testsuite = Dict(
        "throwing test" => quote
            error("This test throws an error")
        end
    )
    error_line = @__LINE__() - 3

    io = IOBuffer()
    @test_throws Test.FallbackTestSetException("Test run finished with errors") begin
        runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=io, stderr=io)
    end

    str = String(take!(io))
    @test contains(str, r"throwing test .+ failed at")
    @test contains(str, "$(basename(@__FILE__)):$error_line")
    @test contains(str, "FAILURE")
    @test contains(str, "Error During Test")
    @test contains(str, "This test throws an error")
end

@testset "crashing test" begin
    msg = "This test will crash"
    testsuite = Dict(
        "abort" => quote
            println($(msg))
            abort() = ccall(:abort, Nothing, ())
            abort()
        end
    )

    io = IOBuffer()
    ioc = IOContext(io, :color => true)
    @test_throws Test.FallbackTestSetException("Test run finished with errors") begin
        runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=ioc, stderr=ioc)
    end

    str = String(take!(io))
    @test contains(str, "Output generated during execution of '\e[31mabort\e[39m':")
    # Make sure we can capture the output generated by the crashed process, see
    # issue <https://github.com/JuliaTesting/ParallelTestRunner.jl/issues/83>.
    @test contains(str, msg)
    # "in expression starting at" comes from the abort trap, make sure we
    # captured that as well.
    @test contains(str, "in expression starting at")
    # Following are messages printed by ParallelTestRunner.
    @test contains(str, r"abort .+ started at")
    @test contains(str, r"abort.+ crashed at")
    @test contains(str, "FAILURE")
    @test contains(str, "Error During Test")
    @test contains(str, "Malt.TerminatedWorkerException")
end

@testset "worker task failure detected by monitor" begin
    testsuite = Dict(
        "a" => :( @test true ),
    )

    exception = ErrorException("test_worker exploded")
    # A bad test_worker will cause the worker task to error out.  With this test
    # we want to make sure the task monitoring system catches and handles it.
    test_worker(name) = throw(exception)

    io = IOBuffer()
    try
        runtests(ParallelTestRunner, ["--jobs=1"];
                 test_worker, testsuite, stdout=io, stderr=io)
        # The `runtests` above should handle the error, so we shouldn't get here
        @test false
    catch e
        @test typeof(e) === TaskFailedException
        @test first(Base.current_exceptions(e.task)).exception == exception
    end
    str = String(take!(io))
    @test contains(str, "Caught an error, stopping...")
    @test !contains(str, "SUCCESS")
    # Not even FAILURE is printed in this case, we exit very early.
    @test !contains(str, "FAILURE")
end

@testset "test output" begin
    msg = "This is some output from the test"
    testsuite = Dict(
        "output" => quote
            println($(msg))
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"output .+ started at")
    @test contains(str, msg)
    @test contains(str, "SUCCESS")

    msg2 = "More output"
    testsuite = Dict(
        "verbose-1" => quote
            print($(msg))
        end,
        "verbose-2" => quote
            println($(msg2))
        end,
        "silent" => quote
            @test true
        end,
    )
    io = IOBuffer()
    # Run all tests on the same worker, makre sure all the output is captured
    # and attributed to the correct test set.
    runtests(ParallelTestRunner, ["--verbose", "--jobs=1"]; testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"verbose-1 .+ started at")
    @test contains(str, r"verbose-2 .+ started at")
    @test contains(str, r"silent .+ started at")
    @test contains(str, "Output generated during execution of 'verbose-1':\n[ $(msg)")
    @test contains(str, "Output generated during execution of 'verbose-2':\n[ $(msg2)")
    @test !contains(str, "Output generated during execution of 'silent':")
    @test contains(str, "SUCCESS")
end

@testset "warnings" begin
    testsuite = Dict(
        "warning" => quote
            @test_warn "3.0" @warn "3.0"
        end
    )

    io = IOBuffer()
    runtests(ParallelTestRunner, ["--verbose"]; testsuite, stdout=io, stderr=io)

    str = String(take!(io))
    @test contains(str, r"warning .+ started at")
    @test contains(str, "SUCCESS")
end

# Issue <https://github.com/JuliaTesting/ParallelTestRunner.jl/issues/69>.
@testset "colorful output" begin
    testsuite = Dict(
        "color" => quote
            printstyled("Roses Are Red"; color=:red)
        end
    )
    io = IOBuffer()
    ioc = IOContext(io, :color => true)
    runtests(ParallelTestRunner, String[]; testsuite, stdout=ioc, stderr=ioc)
    str = String(take!(io))
    @test contains(str, "\e[31mRoses Are Red\e[39m\n")
    @test contains(str, "SUCCESS")

    testsuite = Dict(
        "no color" => quote
            print("Violets are ")
            printstyled("blue"; color=:blue)
        end
    )
    io = IOBuffer()
    ioc = IOContext(io, :color => false)
    runtests(ParallelTestRunner, String[]; testsuite, stdout=ioc, stderr=ioc)
    str = String(take!(io))
    @test contains(str, "Violets are blue\n")
    @test contains(str, "SUCCESS")
end

@testset "reuse of workers" begin
    testsuite = Dict(
        "a" => :(),
        "b" => :(),
        "c" => :(),
        "d" => :(),
        "e" => :(),
        "f" => :(),
    )
    io = IOBuffer()
    ioc = IOContext(io, :color => true)
    old_id_counter = ParallelTestRunner.ID_COUNTER[]
    njobs = 1
    runtests(ParallelTestRunner, ["--jobs=$(njobs)"]; testsuite, stdout=ioc, stderr=ioc)
    str = String(take!(io))
    @test contains(str, "Running $(length(testsuite)) tests using $(njobs) parallel jobs")
    @test ParallelTestRunner.ID_COUNTER[] == old_id_counter + njobs
end


# Issue <https://github.com/JuliaTesting/ParallelTestRunner.jl/issues/106>.
@testset "default workers stopped at end" begin
    # Use default workers (no test_worker) so the framework creates and should stop them.
    # More tests than workers so some tasks finish early and must stop their worker.
    testsuite = Dict(
        "t1" => :(),
        "t2" => :(),
        "t3" => :(),
        "t4" => :(),
        "t5" => :(),
        "t6" => quote
            # Make this test run longer than the others so that it runs alone...
            sleep(5)
            children = _count_child_pids($(getpid()))
            # ...then check there's only one worker still running. WARNING: this test may be
            # flaky on very busy systems, if at this point some of the other tests are still
            # running, hope for the best.
            if children >= 0
                @test children == 1
            end
        end,
    )
    before = _count_child_pids()
    if before < 0
        # Counting child PIDs not supported on this platform
        @test_skip false
    else
        old_id_counter = ParallelTestRunner.ID_COUNTER[]
        njobs = 2
        io = IOBuffer()
        ioc = IOContext(io, :color => true)
        try
            runtests(ParallelTestRunner, ["--jobs=$(njobs)", "--verbose"];
                     testsuite, stdout=ioc, stderr=ioc, init_code=:(include($(joinpath(@__DIR__, "utils.jl")))))
        catch
            # Show output in case of failure, to help debugging.
            output = String(take!(io))
            printstyled(stderr, "Output of failed test >>>>>>>>>>>>>>>>>>>>\n", color=:red, bold=true)
            println(stderr, output)
            printstyled(stderr, "End of output <<<<<<<<<<<<<<<<<<<<<<<<<<<<\n", color=:red, bold=true)
            rethrow()
        end
        # Make sure we didn't spawn more workers than expected.
        @test ParallelTestRunner.ID_COUNTER[] == old_id_counter + njobs
        # Allow a moment for worker processes to exit
        for _ in 1:50
            sleep(0.1)
            after = _count_child_pids()
            after >= 0 && after <= before && break
        end
        after = _count_child_pids()
        @test after >= 0
        @test after == before
    end
end

# Custom workers are handled differently:
# <https://github.com/JuliaTesting/ParallelTestRunner.jl/pull/107#issuecomment-3980645143>.
# But we still want to make sure they're terminated at the end.
@testset "custom workers stopped at end" begin
    testsuite = Dict(
        "a" => :(),
        "b" => :(),
        "c" => :(),
        "d" => :(),
        "e" => :(),
        "f" => :(),
    )
    procs = Base.Process[]
    procs_lock = ReentrantLock()
    function test_worker(name)
        wrkr = addworker()
        Base.@lock procs_lock push!(procs, wrkr.w.proc)
        return wrkr
    end
    runtests(ParallelTestRunner, Base.ARGS; test_worker, testsuite, stdout=devnull, stderr=devnull)
    @test all(!Base.process_running, procs)
end

# ── Unit tests for internal helpers ──────────────────────────────────────────

@testset "extract_flag!" begin
    args = ["--verbose", "--jobs=4", "test1"]
    result = ParallelTestRunner.extract_flag!(args, "--verbose")
    @test result === Some(nothing)
    @test args == ["--jobs=4", "test1"]

    args = ["--verbose", "--jobs=4", "test1"]
    result = ParallelTestRunner.extract_flag!(args, "--jobs"; typ=Int)
    @test something(result) == 4
    @test args == ["--verbose", "test1"]

    args = ["--verbose", "test1"]
    result = ParallelTestRunner.extract_flag!(args, "--jobs")
    @test result === nothing
    @test args == ["--verbose", "test1"]

    args = ["--format=json"]
    result = ParallelTestRunner.extract_flag!(args, "--format")
    @test something(result) == "json"
    @test isempty(args)
end

@testset "parse_args" begin
    @testset "individual flags" begin
        args = parse_args(["--verbose"])
        @test args.verbose !== nothing
        @test args.jobs === nothing
        @test args.quickfail === nothing
        @test args.list === nothing
        @test isempty(args.positionals)

        args = parse_args(["--jobs=4"])
        @test something(args.jobs) == 4
        @test args.verbose === nothing

        args = parse_args(["--quickfail"])
        @test args.quickfail !== nothing
        @test args.verbose === nothing

        args = parse_args(["--list"])
        @test args.list !== nothing
    end

    @testset "combined flags" begin
        args = parse_args(["--verbose", "--quickfail", "--jobs=2"])
        @test args.verbose !== nothing
        @test args.quickfail !== nothing
        @test something(args.jobs) == 2
    end

    @testset "positional arguments" begin
        args = parse_args(["--verbose", "basic", "subdir"])
        @test args.verbose !== nothing
        @test args.positionals == ["basic", "subdir"]

        args = parse_args(["test1", "test2"])
        @test args.positionals == ["test1", "test2"]
    end

    @testset "custom arguments" begin
        args = parse_args(["--gpu", "--nocuda"]; custom=["gpu", "nocuda", "other"])
        @test args.custom["gpu"] !== nothing
        @test args.custom["nocuda"] !== nothing
        @test args.custom["other"] === nothing
    end

    @testset "unknown flags" begin
        @test_throws ErrorException parse_args(["--unknown-flag"])
        @test_throws ErrorException parse_args(["--verbose", "--bogus"])
    end

    @testset "no arguments" begin
        args = parse_args(String[])
        @test args.jobs === nothing
        @test args.verbose === nothing
        @test args.quickfail === nothing
        @test args.list === nothing
        @test isempty(args.positionals)
        @test isempty(args.custom)
    end
end

@testset "filter_tests!" begin
    @testset "empty positionals preserves all tests" begin
        testsuite = Dict("a" => :(), "b" => :(), "c" => :())
        args = parse_args(String[])
        @test filter_tests!(testsuite, args) == true
        @test length(testsuite) == 3
    end

    @testset "startswith matching" begin
        testsuite = Dict("basic" => :(), "advanced" => :(), "basic_extra" => :())
        args = parse_args(["basic"])
        @test filter_tests!(testsuite, args) == false
        @test haskey(testsuite, "basic")
        @test haskey(testsuite, "basic_extra")
        @test !haskey(testsuite, "advanced")
    end

    @testset "multiple positional filters" begin
        testsuite = Dict("unit/a" => :(), "unit/b" => :(), "integration/c" => :(), "perf/d" => :())
        args = parse_args(["unit", "integration"])
        @test filter_tests!(testsuite, args) == false
        @test haskey(testsuite, "unit/a")
        @test haskey(testsuite, "unit/b")
        @test haskey(testsuite, "integration/c")
        @test !haskey(testsuite, "perf/d")
    end

    @testset "no matches yields empty suite" begin
        testsuite = Dict("a" => :(), "b" => :())
        args = parse_args(["nonexistent"])
        @test filter_tests!(testsuite, args) == false
        @test isempty(testsuite)
    end
end

@testset "find_tests edge cases" begin
    @testset "empty directory" begin
        mktempdir() do dir
            @test isempty(find_tests(dir))
        end
    end

    @testset "only runtests.jl" begin
        mktempdir() do dir
            write(joinpath(dir, "runtests.jl"), "@test true")
            @test isempty(find_tests(dir))
        end
    end

    @testset "nested subdirectories" begin
        mktempdir() do dir
            mkpath(joinpath(dir, "a", "b"))
            write(joinpath(dir, "test1.jl"), "@test true")
            write(joinpath(dir, "a", "test2.jl"), "@test true")
            write(joinpath(dir, "a", "b", "test3.jl"), "@test true")
            ts = find_tests(dir)
            @test length(ts) == 3
            @test haskey(ts, "test1")
            @test haskey(ts, "a/test2")
            @test haskey(ts, "a/b/test3")
        end
    end

    @testset "non-.jl files ignored" begin
        mktempdir() do dir
            write(joinpath(dir, "test.jl"), "@test true")
            write(joinpath(dir, "readme.md"), "# Readme")
            write(joinpath(dir, "data.csv"), "1,2,3")
            ts = find_tests(dir)
            @test length(ts) == 1
            @test haskey(ts, "test")
        end
    end
end

@testset "get_max_worker_rss" begin
    rss = withenv("JULIA_TEST_MAXRSS_MB" => nothing) do
        ParallelTestRunner.get_max_worker_rss()
    end
    @test rss > 0

    rss = withenv("JULIA_TEST_MAXRSS_MB" => "1024") do
        ParallelTestRunner.get_max_worker_rss()
    end
    @test rss == 1024 * 2^20
end

@testset "test_exe" begin
    exe = ParallelTestRunner.test_exe(false)
    @test any(contains("--color=no"), exe.exec)
    @test any(contains("--project="), exe.exec)

    exe = ParallelTestRunner.test_exe(true)
    @test any(contains("--color=yes"), exe.exec)
end

# ── Integration tests ────────────────────────────────────────────────────────

@testset "non-verbose mode" begin
    testsuite = Dict("quiet" => quote @test true end)
    io = IOBuffer()
    runtests(ParallelTestRunner, String[]; testsuite, stdout=io, stderr=io)
    str = String(take!(io))
    @test !contains(str, "started at")
    @test !contains(str, "Available memory:")
    @test contains(str, "SUCCESS")
end

@testset "positional filter end-to-end" begin
    testsuite = Dict(
        "unit/math" => :( @test 1 + 1 == 2 ),
        "unit/string" => :( @test "a" * "b" == "ab" ),
        "integration/api" => :( @test true ),
    )
    io = IOBuffer()
    runtests(ParallelTestRunner, ["unit"]; testsuite, stdout=io, stderr=io)
    str = String(take!(io))
    @test contains(str, "Running 2 tests")
    @test contains(str, "SUCCESS")
end

@testset "addworkers" begin
    workers = addworkers(2)
    @test length(workers) == 2
    @test all(w -> w isa ParallelTestRunner.PTRWorker, workers)
    @test all(w -> Base.process_running(w.w.proc), workers)
    for w in workers
        ParallelTestRunner.Malt.stop(w)
    end
    sleep(0.5)
    @test all(w -> !Base.process_running(w.w.proc), workers)
end

@testset "multiple tests multiple jobs" begin
    testsuite = Dict(
        "m1" => :( @test 1 + 1 == 2 ),
        "m2" => :( @test 2 + 2 == 4 ),
        "m3" => :( @test 3 + 3 == 6 ),
        "m4" => :( @test 4 + 4 == 8 ),
    )
    io = IOBuffer()
    runtests(ParallelTestRunner, ["--jobs=2"]; testsuite, stdout=io, stderr=io)
    str = String(take!(io))
    @test contains(str, "Running 4 tests using 2 parallel jobs")
    @test contains(str, "SUCCESS")
end

@testset "worker RSS recycling" begin
    testsuite = Dict(
        "alloc1" => :( @test true ),
        "alloc2" => :( @test true ),
        "alloc3" => :( @test true ),
        "alloc4" => :( @test true ),
    )
    io = IOBuffer()
    old_id_counter = ParallelTestRunner.ID_COUNTER[]
    runtests(ParallelTestRunner, ["--jobs=1"]; testsuite, stdout=io, stderr=io, max_worker_rss=0)
    str = String(take!(io))
    @test contains(str, "SUCCESS")
    @test ParallelTestRunner.ID_COUNTER[] == old_id_counter + length(testsuite)
end

@testset "mixed pass and fail" begin
    testsuite = Dict(
        "passes" => quote
            @test true
            @test 1 + 1 == 2
        end,
        "also_passes" => quote
            @test true
        end,
        "fails" => quote
            @test false
        end,
    )
    io = IOBuffer()
    @test_throws Test.FallbackTestSetException begin
        runtests(ParallelTestRunner, String[]; testsuite, stdout=io, stderr=io)
    end
    str = String(take!(io))
    @test contains(str, "FAILURE")
    @test contains(str, "passes")
    @test contains(str, "also_passes")
    @test contains(str, "fails")
end

@testset "empty test suite" begin
    testsuite = Dict{String,Expr}()
    io = IOBuffer()
    runtests(ParallelTestRunner, String[]; testsuite, stdout=io, stderr=io)
    str = String(take!(io))
    @test contains(str, "Running 0 tests")
    @test contains(str, "SUCCESS")
end

# This testset should always be the last one, don't add anything after this.
# We want to make sure there are no running workers at the end of the tests.
@testset "no workers running" begin
    children = _count_child_pids()
    if children >= 0
        @test children == 0
    end
end

end
