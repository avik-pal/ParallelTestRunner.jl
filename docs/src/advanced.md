```@setup mypackage
using ParallelTestRunner
using MyPackage
test_dir = joinpath(pkgdir(ParallelTestRunner), "docs", "MyPackage", "test")
```

# Advanced Usage

```@meta
CurrentModule = ParallelTestRunner
DocTestSetup = quote
    using ParallelTestRunner
end
```

This page covers advanced features of `ParallelTestRunner` for customizing test execution.

## Customizing the test suite

By default, [`runtests`](@ref) automatically discovers all `.jl` files in your `test/` directory (excluding `runtests.jl` itself) using the `find_tests` function.
You can customize which tests to run by providing a custom `testsuite` dictionary:

```@example mypackage
using ParallelTestRunner
using MyPackage

# Manually define your test suite
testsuite = Dict(
    "basic" => quote
        include(joinpath(@__DIR__, "basic.jl"))
    end,
    "advanced" => quote
        include(joinpath(@__DIR__, "advanced.jl"))
        @test 40 + 2 ≈ 42
    end
)

cd(test_dir) do # hide
runtests(MyPackage, ARGS; testsuite)
end # hide
```

## Filtering Test Files

You can also use [`find_tests`](@ref) to automatically discover test files and then filter or modify them.
This requires manually parsing arguments so that filtering is only applied when the user did not request specific tests to run:

```@example mypackage
using ParallelTestRunner
using MyPackage

# Start with autodiscovered tests
cd(test_dir) do # hide
testsuite = find_tests(@__DIR__)

# Parse arguments
args = parse_args(ARGS)

if filter_tests!(testsuite, args)
    # Remove tests that shouldn't run on non-Windows systems
    if !Sys.iswindows()
        delete!(testsuite, "advanced")
    end
end

runtests(MyPackage, args; testsuite)
end # hide
```

The [`filter_tests!`](@ref) function returns `true` if no positional arguments were provided (allowing additional filtering) and `false` if the user specified specific tests (preventing further filtering).

## Initialization Code

Use the `init_code` keyword argument to [`runtests`](@ref) to provide code that runs before each test file.
This is useful for:
- Importing packages
- Defining constants, defaults or helper functions
- Setting up test infrastructure

```@example mypackage
using ParallelTestRunner
using MyPackage

const init_code = quote
    # Define a helper function available to all tests
    function test_helper(x)
        return x * 2
    end
end

cd(test_dir) do # hide
runtests(MyPackage, ARGS; init_code)
end # hide
```

The `init_code` is evaluated in each test's sandbox module, so all definitions are available to your test files.

## Worker Initialization

For most situations, `init_code` described above should be used. However, if the common code takes so long to import that it makes a notable difference to run before every testset, you can use the `init_worker_code` keyword argument in [`runtests`](@ref) to have it run only once at worker initialization. However, you will also have to import the directly-used functionality in your testset module using `init_code` due to the way ParallelTestRunner.jl creates a temporary module for each testset.

The example below is trivial and `init_worker_code` would not be necessary if this were used in a package, but it shows how it should be used. A real use-case of this is for tests using the GPUArrays.jl test suite; including it takes about 3s, so that 3s running before every testset can add a significant amount of runtime to the various GPU backend testsuites as opposed to running once when the runner is initally created.

```@example mypackage
using ParallelTestRunner
using MyPackage

const init_worker_code = quote
    # Common code that's slow to import
    function complex_common_test_helper(x)
        return x * 2
    end
end

const init_code = quote
    # ParallelTestRunner creates a temporary module to run
    #  each testset. `init_code` runs in this temporary module,
    #  but code from `init_worker_code` that will be directly
    #  called in a testset must be explicitly included in the
    #  module namespace.
    import ..complex_common_test_helper
end

cd(test_dir) do # hide
runtests(MyPackage, ARGS; init_worker_code, init_code)
end # hide
```
The `init_worker_code` is evaluated once per worker, so all definitions can be imported for use by the test module.

## Serial Tests

Some tests cannot safely run in parallel with other tests — for example, tests that allocate very large arrays and would exhaust memory if multiple ran simultaneously.
The `serial` keyword argument to [`runtests`](@ref) lets you designate specific tests to run one at a time, while the remaining tests still run in parallel.

```@example mypackage
using ParallelTestRunner
using MyPackage

testsuite = Dict(
    "big_alloc" => quote
        # This test allocates ~4 GB and should not overlap with other tests
        @test true
    end,
    "huge_matrix" => quote
        @test true
    end,
    "fast_unit" => quote
        @test 1 + 1 == 2
    end,
    "fast_integration" => quote
        @test true
    end,
)

# "big_alloc" and "huge_matrix" run one at a time; the rest run in parallel
runtests(MyPackage, ["--verbose"]; testsuite, serial=["big_alloc", "huge_matrix"])
```

By default serial tests run **before** the parallel batch.
Use `serial_position=:after` to run them after instead:

```@example mypackage
runtests(MyPackage, ["--verbose"]; testsuite, serial=["big_alloc", "huge_matrix"], serial_position=:after)
```

Serial tests participate in the same ordering logic as parallel tests (sorted by historical
duration, longest first) and their results appear in the same overall summary.

!!! tip
    With automatic test discovery via [`find_tests`](@ref), the `serial` names are the same
    keys that appear in the testsuite dictionary (e.g. `"subdir/memory_test"`).

!!! note
    If the user filters tests via positional arguments (e.g. `julia test/runtests.jl unit`),
    any serial test names that were filtered out are silently removed from the serial list.

## Failure Handling

Both options described in this section are opt-in and default to off.

### Recycling Workers after a Failure

Workers are reused across tests, so a test that corrupts process-wide state — a wedged GPU driver whose every subsequent allocation fails, a global left in an inconsistent state, a library put in an unusable configuration — can make every later test scheduled on that same worker fail too.

Setting `recycle_on_failure=true` stops the worker after any test that did not pass, so the next test gets a fresh process:

```julia
runtests(MyPackage, ARGS; recycle_on_failure=true)
```

This complements the existing recycling of workers exceeding `max_worker_rss` and of workers that crashed outright.

### Retrying Failed Tests

When several workers compete for a limited resource (usually memory), a failure can mean "lost the race for the resource" rather than "the code is broken".
Such a test typically passes when run on its own.

The `retries` keyword argument re-runs tests that did not pass, up to `N` times, after the main run has completed:

```julia
runtests(MyPackage, ARGS; retries=1)
```

Retried tests run **sequentially on a single fresh worker**, so a test that failed only because of concurrent resource pressure gets an otherwise-idle system.
If a test fails again, its worker is stopped before the next retry, so one failure cannot contaminate the following one.

Only the final attempt of each test is recorded in the results, so a test that passes on retry is reported as passing and a persistently broken test is reported as failing.
Retries are visible in the output, so flakiness is surfaced rather than hidden:

```
Retrying 1 failed test  (1)
fails      (8) │     0.05 │   failed at 2026-08-08T15:10:15.526
```

While a test still has an attempt left, its failure is printed in yellow, and the final
attempt is printed in red. A red line therefore always marks the result that will be reported,
and a yellow one marks a result that may still be replaced.

!!! note
    Retries are skipped when the run was interrupted (e.g. `Ctrl+C`) or when `--quickfail` is
    in effect, since in both cases the run stopped early on purpose.

!!! tip
    `recycle_on_failure` and `retries` address different halves of the same problem and work
    well together: recycling keeps one bad test from cascading onto its worker during the run,
    while retries give the tests that did fail a contention-free second chance.

## Memory Pressure on macOS

On memory-constrained macOS machines (notably CI runners), requesting more jobs than the default can make the test suite take much longer than expected, sometimes enough to time out the job.
This often manifests as per-test init times (shown with `--verbose`) steadily increasing over the run, likely because macOS compresses memory under pressure and each garbage collection gets slower. GC % being higer than usual can also be an indication that you're requesting too many jobs or that the max RSS threshold is too high.
Prefer the default `--jobs` value, which accounts for available memory, and lower the `JULIA_TEST_MAXRSS_MB` environment variable so that workers get recycled sooner. See [issue #124](https://github.com/JuliaTesting/ParallelTestRunner.jl/issues/124) for more details.

## Custom Workers

For tests that require specific environment variables or Julia flags, you can use the `test_worker` keyword argument to [`runtests`](@ref) to assign tests to custom workers:

```@example mypackage
using ParallelTestRunner
using MyPackage

function test_worker(name)
    if name == "needs_env_var"
        # Create a worker with a specific environment variable
        return addworker(; env = ["SPECIAL_ENV_VAR" => "42"])
    elseif name == "needs_threads"
        # Create a worker with multiple threads
        return addworker(; exeflags = ["--threads=4"])
    end
    # Return nothing to use the default worker
    return nothing
end

testsuite = Dict(
    "needs_env_var" => quote
        @test ENV["SPECIAL_ENV_VAR"] == "42"
    end,
    "needs_threads" => quote
        @test Base.Threads.nthreads() == 4
    end,
    "normal_test" => quote
        @test 1 + 1 == 2
    end
)

runtests(MyPackage, ARGS; test_worker, testsuite)
```

The `test_worker` function receives the test name and should return either:
- A worker object (from [`addworker`](@ref)) for tests that need special configuration
- `nothing` to use the default worker pool

!!! note
    If your test suite uses both a `test_worker` function and `init_worker_code` as described in a prior section,
    `test_worker` must also take in `init_worker_code` as a second argument. You are responsible for passing it to
    [`addworker`](@ref) if your `init_code` depends on any `init_worker_code` definitions.

## Custom Arguments

If your package needs to accept its own command-line arguments in addition to `ParallelTestRunner`'s options, use [`parse_args`](@ref) with custom flags:

```@example mypackage
using ParallelTestRunner
using MyPackage

# Parse arguments with custom flags
args = parse_args(ARGS; custom=["myflag", "another-flag"])

# Access custom flags
if args.custom["myflag"] !== nothing
    println("Custom flag was set!")
end

# Pass parsed args to runtests
cd(test_dir) do # hide
runtests(MyPackage, args)
end # hide
```

Custom flags are stored in the `custom` field of the [`ParsedArgs`](@ref) object, with values of `nothing` (not set) or `Some(value)` (set, with optional value).

## Interactive use

Arguments can also be passed via the standard [`Pkg.test`](https://pkgdocs.julialang.org/v1/api/#Pkg.test) interface for interactive control.
For example, here is how we could run the subset of test files that start with the name `test_cool_feature` in i) verbose mode, and ii) with a specific number of Julia threads enabled:

```bash
# Start julia in an environment where `MyPackage.jl` is available
julia --project
```
```@repl mypackage
using Pkg

# No need to start a fresh session to change threading
Pkg.test("MyPackage"; test_args=`--verbose advanced`, julia_args=`--threads=auto`);
```

Alternatively, arguments can be passed directly from the command line with a shell alias like the one below:

```julia-repl
jltest --threads=auto -- --verbose test_cool_feature
```

Shell alias:

```bash
function jltest {
    julia=(julia)

    # certain arguments (like those beginnning with a +) need to come first
    if [[ $# -gt 0 && "$1" = +* ]]; then
        julia+=("$1")
        shift
    fi

    "${julia[@]}" --startup-file=no --project -e "using Pkg; Pkg.API.test(; test_args=ARGS)" "$@"
}
```

## Best Practices

1. **Keep tests isolated**: Each test file runs in its own module, so avoid relying on global state between tests.

1. **Use `init_code` for common setup**: Instead of duplicating setup code in each test file, use `init_code` to share common initialization. For long-running initialization, consider using `init_worker_code` so that it is run only once per worker creation instead of before each test.

1. **Filter tests appropriately**: Use [`filter_tests!`](@ref) to respect user-specified test filters while allowing additional programmatic filtering.

1. **Handle platform differences**: Use conditional logic in your test suite setup to handle platform-specific tests:

   ```julia
   testsuite = find_tests(@__DIR__)
   if Sys.iswindows()
       delete!(testsuite, "unix_specific_test")
   end
   ```

1. **Load balance the test files**: `ParallelTestRunner` runs the tests files in parallel, ideally all test files should run for _roughly_ the same time for better performance.
   Having few long-running test files and other short-running ones hinders scalability.

1. **Use custom workers sparingly**: Custom workers add overhead. Only use them when tests genuinely require different configurations.

1. **Use `serial` for resource-intensive tests**: If a test allocates significant memory or uses exclusive hardware resources, mark it as serial rather than reducing `--jobs` globally. This keeps the rest of your suite running in parallel.

1. **Only use `retries` for worker contention-related failures**: Not all intermittent failures are caused by parallel worker resource contention. Ensure you aren't masking real test failures when using this feature.

1. **Don't request too many jobs on low-memory macOS machines**: tests can take much longer than expected; see [Memory Pressure on macOS](@ref).
